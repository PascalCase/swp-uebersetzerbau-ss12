<program> ::= <funcs>
<funcs> ::= <func> <funcs> | "@"
<func> ::= "def" <type> "id" "(" <optparams> ")" <func'>
<func'> ::= <block> | ";"
<optparams> ::= <params> | "@"
<params> ::= <type> "id" <params'>
<params'> ::= "," <params> | "@"
<block> ::= "{" <decls> <stmts> "}"
<decls> ::= <decl> <decls> | "@"
<decl> ::= <type> "id" ";"
<type> ::= "basic" <type'> | "record" "{" <decls> "}" <type'>
<type'> ::= "[" "num" "]" <type'> | "@"
<stmts> ::= <stmt> <stmts> | "@"

<stmt> ::= <assign> ";" | "if" "(" <assign> ")" <stmt> <stmt'> | "while" "(" <assign> ")" <stmt> | "do" <stmt> "while" "(" <assign> ")" ";" | "break" ";" | "return" <stmt''> | "print" <loc> ";" | <block>
<stmt'> ::= "else" <stmt> | "@"
<stmt''> ::= <loc> ";" | ";"
<loc> ::= "id" <loc''>
<loc'> ::= "." "id" | "[" <assign> "]"
<loc''> ::= <loc'> <loc''> | "@"

<assign> ::= <bool> <assign'>
<assign'> ::= "=" <assign> <assign'> | "@"
<bool> ::= <join> <bool'>
<bool'> ::= "||" <join> <bool'> | "@"
<join> ::= <equality> <join'>
<join'> ::= "&&" <equality> <join'> | "@"
<equality> ::= <rel> <equality'>
<equality'> ::= "==" <rel> <equality'> | "!=" <rel> <equality'> | "@"
<rel> ::= <expr> <rel'>
<rel'> ::= "<" <expr> | "<=" <expr> | ">=" <expr> | ">" <expr> | "@"
<expr> ::= <term> <expr'>
<expr'> ::= "+" <term> <expr'> | "-" <term> <expr'> | "@"
<unary> ::= "!" <unary> | "-" <unary> | <factor>
<factor> ::= "(" <assign> ")" | <loc> <factor'> | "num" | "real" | "true" | "false" | "string"
<factor'> ::= "(" <optargs> ")" | "@"
<optargs> ::= <args> | "@"
<args> ::= <assign> <args'>
<args'> ::= "," <args> | "@"